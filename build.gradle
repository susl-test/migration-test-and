apply plugin: 'maven-publish'

def sdkVersion = '0.0.24'
def aarFile = file("${projectDir}/libs/sdk-${sdkVersion}.aar")

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:1.8.0"
    api 'androidx.core:core-ktx:1.15.0'
    api 'androidx.webkit:webkit:1.12.1'
}

afterEvaluate {
    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            tasks.register("attachAar${variant.name.capitalize()}", Copy) {
                from(aarFile)
                into(output.outputFile.parent)
                rename { fileName -> "${project.name}-${variant.name}.aar" }
            }
            variant.assembleProvider.configure { finalizedBy tasks["attachAar${variant.name.capitalize()}"] }
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'com.github.inbrainai'
            artifactId 'sdk-android'
            version = sdkVersion
            artifact(aarFile)
        }
    }
}
