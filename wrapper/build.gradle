apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.webkit:webkit:1.12.1'
}

def sdkVersion = '0.0.24'
android {
    compileSdk 35

    defaultConfig {
        minSdk 21
        targetSdk 35
        versionCode 1
        versionName sdkVersion
    }

    buildTypes {
        release {
            minifyEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    publishing {
        singleVariant("release") {
        }
    }
    namespace 'com.inbrain.wrapper'
    buildFeatures {
        buildConfig true
    }
}

def aarFile = file("../libs/sdk-${sdkVersion}.aar")

def releaseAar = file("$projectDir/build/outputs/aar/wrapper-release.aar")

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId 'com.github.inbrainai'
                artifactId 'sdk-android'
                version = sdkVersion

                from components.release
                artifact(releaseAar) {
                    builtBy tasks.named("replaceReleaseAar")
                    extension = 'aar'
                    classifier = ''
                }
            }
        }
    }
}

tasks.register("replaceReleaseAar") {
    dependsOn tasks.named("assembleRelease")
    doLast {
        copy {
            from aarFile
            into releaseAar.parentFile
            rename { releaseAar.name }
        }
    }
}